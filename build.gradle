apply plugin: 'base'
apply plugin: 'maven-publish'

version = '1.0.0'
group = 'io.github.cruciblemc'

publishing {
    publications {
        create("maven", MavenPublication) {
            artifactId = 'omniconfig'

            pom {
                name.set('Omniconfig')
                description.set('Version-independent mod configuration API')
                url.set('https://github.com/CrucibleMC/Omniconfig')

                licenses {
                    license {
                        name.set('Lesser GNU Public License, Version 2.1')
                        url.set('https://www.gnu.org/licenses/lgpl-2.1.html')
                    }
                }

                scm {
                    url.set("https://github.com/CrucibleMC/Omniconfig")
                    connection.set("scm:git:git://github.com/CrucibleMC/Omniconfig.git")
                    developerConnection.set("scm:git:git@github.com:CrucibleMC/Omniconfig.git")
                }
            }
        }
    }

    repositories {
        maven {
            url = buildDir.absolutePath + "/repo"
            name = "filesystem"
        }
    }
}

var buildTimestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
subprojects {
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenLocal()
    }

    afterEvaluate {
        dependencies {
            compileOnly 'org.jetbrains:annotations:20.1.0'
        }

        jar {
            manifest {
                attributes([
                        "Specification-Title"      : rootProject.name,
                        "Specification-Vendor"     : "CrucibleMC Team",
                        "Specification-Version"    : "1",
                        "Implementation-Title"     : rootProject.name + '-' + project.name,
                        "Implementation-Version"   : project.version,
                        "Implementation-Vendor"    : "CrucibleMC Team",
                        "Implementation-Timestamp" : buildTimestamp,
                ])
            }
        }

        // TODO: Better way of doing this????
        configurations.archives.allArtifacts.forEach {
            rootProject.extensions.publishing.publications.maven.artifact it
        }
    }
}
